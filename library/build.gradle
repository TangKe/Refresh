apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

android {
    compileSdkVersion versions.compileSdkVersion

    defaultConfig {
        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.targetSdkVersion
        versionCode versions.versionCode
        versionName versions.versionName
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

task sourceJar(type: Jar) {
    archiveClassifier = 'sources'
    android.libraryVariants.all {
        from it.sourceSets.java.srcDirs
    }
}

afterEvaluate {
    Properties properties = new Properties()
    properties.load(rootProject.file('local.properties').newDataInputStream())
    String mavenUsername = properties.getProperty('maven.user')
    String mavenPassword = properties.getProperty('maven.password')
    String repository = properties.getProperty('maven.repository')
    Boolean includeSource = Boolean.parseBoolean(properties.getProperty('maven.includeSource'))
    publishing {
        repositories {
            maven {
                url = repository
                credentials {
                    username = mavenUsername
                    password = mavenPassword
                }
            }
        }
        publications {
            maven(MavenPublication) {
                groupId = 'ke.tang'
                artifactId = 'refresh'
                version = versions.versionName
                from components.release
                if (includeSource) {
                    artifact sourceJar
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.appcompat:appcompat:$versions.library.appCompat"
    api "com.airbnb.android:lottie:$versions.library.lottie"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$versions.kotlin"
    implementation "androidx.core:core-ktx:$versions.library.ktx"
}